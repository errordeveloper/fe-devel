#
# Copyright 2010-2011 Fabric Technologies Inc. All rights reserved.
#

import os
Import( 'baseEnv', 'buildOS', 'buildArch', 'archDistDir', 'FABRIC_VERSION', 'fabricPythonModule', 'fabricPythonLib', 'pythonLibraryName', 'browserExtFiles', 'fileSystemExtFiles', 'generateDistZip' )

pythonPackageName = "FabricEngine-PythonModule-"+buildOS+"-"+buildArch+"-"+FABRIC_VERSION

pythonPackageDir = archDistDir.Dir(pythonPackageName)
pythonModuleDir = pythonPackageDir.Dir('python_modules').Dir('fabric')

installerFiles = []

installerFiles.append( baseEnv.Command(
  pythonModuleDir.File('__init__.py'),
  fabricPythonModule,
  [ Copy( '$TARGET', '$SOURCE' ) ]
) )

installerFiles.append( baseEnv.Command(
  pythonModuleDir.File(pythonLibraryName),
  fabricPythonLib,
  [ Copy( '$TARGET', '$SOURCE' ) ]
) )

installerFiles.append( baseEnv.Command(
  pythonPackageDir.File('README.txt'),
  'README.txt',
  [ Copy( '$TARGET', '$SOURCE' ) ]
) )

installerFiles.append( baseEnv.Command(
  pythonPackageDir.File('LICENSE.txt'),
  'LICENSE.txt',
  [ Copy( '$TARGET', '$SOURCE' ) ]
) )

installerFiles.append( Install(
  pythonPackageDir.Dir('Exts'),
  [browserExtFiles, fileSystemExtFiles]
) )

if buildOS == 'Windows':
  installerEnv = baseEnv.Clone()
  generateDistZip( installerEnv )
  installerEnv.Append( DISTZIP_ROOTDIR = pythonPackageDir )
  pythonInstaller = installerEnv.DistZip(
    archDistDir.File( pythonPackageName+".zip" ),
    installerFiles
  )
else: 
  pythonInstaller = baseEnv.Command(
    archDistDir.File(pythonPackageName+".tar.bz2"),
    installerFiles,
    [
      [ "tar", "-C", archDistDir, "-jcf", "$TARGET", pythonPackageName ]
    ]
  )

Alias( 'pythonInstaller', pythonInstaller )
Return( 'pythonInstaller' )
