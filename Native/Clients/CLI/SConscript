#
# Copyright 2010-2011 Fabric Technologies Inc. All rights reserved.
#

import os.path

Import( 'coreEnv', 'buildOS', 'buildType', 'buildObject', 'archDistDir', 'packageName', 'nodeFlags' )

cliEnv = coreEnv.Clone()
cliEnv.MergeFlags( nodeFlags )
if buildOS == 'Darwin':
  cliEnv.Append( LINKFLAGS = ["-undefined", "dynamic_lookup"] )

extraObjects = []

if buildOS == 'Windows':
    Import( 'getoptFlags' )
    cliEnv.MergeFlags( getoptFlags )
    cliEnv.Append( LINKFLAGS = [
        '/IMPLIB:${TARGET.dir}/fabric_cmd.lib',
        '/STACK:4194304'
    ] )

cliLoadableModule = cliEnv.LoadableModule( 'Fabric.node', [Glob('*.cpp'),buildObject,extraObjects] )

nodeModulesDistDir = archDistDir.Dir(packageName).Dir('node_modules')
fabricNodeModuleDistDir = nodeModulesDistDir.Dir('Fabric')
fabricNodePackage = cliEnv.Command(
  fabricNodeModuleDistDir.File('package.json'),
  'package.json',
  [
    Copy( "$TARGET", "$SOURCE" )
  ])
Export('fabricNodePackage')
fabricNodeAddOn = cliEnv.Command(
  fabricNodeModuleDistDir.File('Fabric.node'),
  cliLoadableModule,
  [
    Copy( "$TARGET", "$SOURCE" )
  ])
Export('fabricNodeAddOn')

nodeModuleReadMe = cliEnv.File('README.txt')
Export('nodeModuleReadMe')
nodeModuleLicense = cliEnv.File('LICENSE.txt')
Export('nodeModuleLicense')

binDistDir = archDistDir.Dir(packageName).Dir('bin')
if buildOS == 'Darwin':
  lipoCmd = ['lipo', '-create', '-output', '$TARGET']
  for arch in ['i386', 'x86_64']:
    lipoCmd.append('-arch')
    lipoCmd.append(arch)
    lipoCmd.append( File(os.path.join( '#ThirdParty', 'Private', buildOS, arch, buildType, 'bin', 'node' )).abspath )
  nodeBin = cliEnv.Command(
    binDistDir.File('node'),
    [],
    [lipoCmd]
  )
