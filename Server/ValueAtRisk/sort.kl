function Size partition(
  io Scalar a<>,
  Size begin,
  Size end,
  Size pivot
  )
{
  Scalar piv = a[pivot];
  a[pivot] = a[end-1];
  a[end-1] = piv;
  Size store = begin;
  for (Size i=begin; i<end-1; ++i) {
    if (a[i] <= piv) {
      Scalar t = a[store];
      a[store] = a[i];
      a[i] = t;
      ++store;
    }
  }
  Scalar t = a[end-1];
  a[end-1] = a[store];
  a[store] = t;
  return store;
}

function qsort(
  io Scalar a<>,
  Size begin,
  Size end
  )
{
  if (end - begin <= 1)
    return;
  else {
    Size pivot = partition(a, begin, end, begin+(end-begin)/2);
    qsort(a, begin, pivot);
    qsort(a, pivot+1, end);
  }
}

operator sort(
  io Scalar v<>
  )
{
  qsort(v, 0, v.size);
}
